plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.distributed'
version = '0.0.1-SNAPSHOT'
description = 'Distributed Ledger System with Hexagonal Architecture'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.retry:spring-retry'
    implementation("org.springframework:spring-aspects")

    implementation('org.springframework.kafka:spring-kafka'){
        exclude group: 'org.lz4', module: 'lz4-java'
    }
    implementation 'org.postgresql:postgresql'

    // Flyway for database migrations
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation 'org.redisson:redisson-spring-boot-starter:3.31.0'

    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:kafka'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'integration'
        exclude '**/*IntegrationTest.class'
    }
    jvmArgs('-Djdk.tracePinnedThreads=short')
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform {
        include '**/*IntegrationTest.class'
    }
    description = 'Runs integration tests with Testcontainers'
    group = 'verification'
    jvmArgs('-Djdk.tracePinnedThreads=short')
    shouldRunAfter test

    testLogging {
        events "started", "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat "full"
    }
}

tasks.register('loadTest', Exec) {
    description = 'Runs k6 load tests'
    group = 'verification'
    workingDir 'src/test/java/com/distributed/ledger/k6'
    commandLine 'k6', 'run', 'load-test.js'
    ignoreExitValue = true
}